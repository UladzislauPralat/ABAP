@AbapCatalog.sqlViewName: 'ZIPRCCHGC'
@Analytics.dataCategory: #CUBE
@VDM.viewType: #COMPOSITE
@EndUserText.label: 'Price Change'
@AccessControl.authorizationCheck: #CHECK
define view ZI_PriceChangeCube as select from ZP_PriceChange
  association [0..1] to ZI_TableName            as _TableName             on $projection.TableName  = _TableName.TableName                                                           
  association [0..1] to ZI_FieldName            as _FieldName             on $projection.TableName  = _FieldName.TableName 
                                                                         and $projection.FieldName  = _FieldName.FieldName
  association [0..1] to ZI_ChangeType           as _ChangeType            on $projection.ChangeType = _ChangeType.ChangeType                                                                                                                                   
  association [0..1] to Rv_P_User               as _User                  on $projection.UserName = _User.userName
  association [0..1] to ZI_TransactionCode      as _TransactionCode       on $projection.TransactionCode = _TransactionCode.TransactionCode
  association [0..1] to ZI_ChangeDocFieldValue as _ChangeDocFieldValueOld on $projection.OldValue  = _ChangeDocFieldValueOld.ChangeDocFieldValue
  association [0..1] to ZI_ChangeDocFieldValue as _ChangeDocFieldValueNew on $projection.NewValue  = _ChangeDocFieldValueNew.ChangeDocFieldValue  
  association [0..1] to ZI_ConditionUsage       as _ConditionUsage        on $projection.ConditionUsage = _ConditionUsage.ConditionUsage
  association [0..1] to ZI_ConditionApplication as _ConditionApplication  on $projection.ConditionApplication = _ConditionApplication.ConditionApplication  
  association [0..1] to ZI_ConditionType        as _ConditionType         on $projection.ConditionUsage = _ConditionType.ConditionUsage
                                                                         and $projection.ConditionApplication = _ConditionType.ConditionApplication
                                                                         and $projection.ConditionType = _ConditionType.ConditionType
  association [0..1] to I_Material              as _Material              on $projection.Material = _Material.Material
  association [0..1] to I_SalesOrganization     as _SalesOrganization     on $projection.SalesOrganization = _SalesOrganization.SalesOrganization
  association [0..1] to I_DistributionChannel   as _DistributionChannel   on $projection.DistributionChannel = _DistributionChannel.DistributionChannel
  association [0..1] to I_Customer              as _Customer              on $projection.Customer = _Customer.Customer                                                                     
{
  key ConditionRecordNo,
  key ChangeNumber,
  @ObjectModel.foreignKey.association: '_TableName'  
  key TableName,  
  key TableKey,
  @ObjectModel.foreignKey.association: '_FieldName'  
  key FieldName,  
  @ObjectModel.foreignKey.association: '_ChangeType'    
  key ChangeType,
  @ObjectModel.foreignKey.association: '_User'        
  UserName,
  UdateDate,
  UpdateTime,
  @ObjectModel.foreignKey.association: '_TransactionCode'  
  TransactionCode,
//  OldValue,
 // NewValue,
  
    @ObjectModel.foreignKey.association: '_ChangeDocFieldValueNew'  
  case when FieldName = 'KEY' then '*** Created ***'
       when NewValue = '' and CurrencyNew = '' then ' '
       else cast(ltrim(concat_with_space(NewValue,CurrencyNew,1),' ') as zcdfldvaln) 
  end  as NewValue,
  @ObjectModel.foreignKey.association: '_ChangeDocFieldValueOld'
  cast( case ltrim(concat_with_space(OldValue,CurrencyOld,1),' ')
  when '' then ' '
  else ltrim(concat_with_space(OldValue,CurrencyOld,1),' ') end as zcdfldvalo) as OldValue,
  
  Agreement,
  SalesDeal,
  Promotion,
  @ObjectModel.foreignKey.association: '_ConditionUsage'     
  ConditionUsage,
  @ObjectModel.foreignKey.association: '_ConditionApplication'  
  ConditionApplication,
  @ObjectModel.foreignKey.association: '_ConditionType'   
  ConditionType,
  ConditionTable,
  @ObjectModel.foreignKey.association: '_SalesOrganization'  
  SalesOrganization,
  @ObjectModel.foreignKey.association: '_DistributionChannel'
  DistributionChannel,
  @ObjectModel.foreignKey.association: '_Customer'   
  Customer,
  @ObjectModel.foreignKey.association: '_Material'  
  Material,
  ValidFrom,
  ValidTo,
  ValidityPeriod,
  @Semantics.unitOfMeasure: true
  @EndUserText.label: 'Unit'    
  cast( 'EA' as abap.unit( 3 ) ) as Unit,  
  @Semantics.quantity.unitOfMeasure: 'Unit'
  @DefaultAggregation: #SUM  
  @EndUserText.label: 'Count'  
  1 as PriceChangeCount,       
  _TableName,
  _FieldName,
  _ChangeType,  
  _User,
  _TransactionCode,
  _ChangeDocFieldValueNew,  
  _ChangeDocFieldValueOld,  
  _ConditionUsage,
  _ConditionApplication,  
  _ConditionType,
  _SalesOrganization,  
  _Material,
  _DistributionChannel,
  _Customer    
} 
